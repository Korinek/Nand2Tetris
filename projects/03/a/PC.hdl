// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:

    Inc16(in=registerOut, out=registerIncremented);

    //Mux inputs = a(000) b(001) c(010) d(011) e(100) f(101) g(110) h(111)
    //reset is right bit, load is middle bit, inc is left bit
    Mux8Way16(b=false, d=false, f=false, h=false,
              c=in, g=in,
              e=registerIncremented,
              a=registerOut,
              sel[0]=reset, sel[1]=load, sel[2]=inc,
              out=registerInput);

    //Always load the mux output.
    Register(in=registerInput, load=true, out=out, out=registerOut);
}
